
「------------------ 条件语句测试-----------------------

  测试 "如果{}" "如果{}否则如果{}"  "如果{}否则{}" 语句
  以及 "循环{}" 语句
  并展示了其它一些代码编写的技巧

 ------------------------------------------------------」

∥包含其它文件 打星号的表示是系统定义的文件
∥如果是自定义的文件请使用文件路径全称
∥或者使用与当前文件的相对路径(.\  ..\)
《*oasm32.oah》
《*Kernel32.oah》

∥引用库文件 打星号的表示是系统库文件
∥如果不是系统库文件请使用库文件路径全称
〈*Kernel32.lib〉


∥数据段


字节 字符串1E = "1.数据32 等于 10L"  ∥定义一个字节数组 这里的 "L" 符号表示回车符
字节 字符串2E = "2.数据32 > 10L" 
字节 字符串3E = "3.数据32 小于等于 10\"L"  ∥这里显示了使用转义字符输出双引号的方法
字节 字符串4E = "4.并且条件成立\LL"  ∥这里显示了使用转义字符输出 "L" 符号的方法
字节 字符串5E = "5.并且条件不成立L "  
字节 字符串6E = "6.循环1次L"

双字 整数A ∥定义一个4个字节的变量



∥代码段开始


∥开始函数定义
∏主函数()
{ 
  ∥这里添加代码

  ∥注意：测试过程中会调用到函数，函数可能会更改寄存器的值
  ∥因此在使用寄存器测试时一定要在条件之前对寄存器重新赋值，否则可能会得不到预期的结果
  
  数据32 = 10
  ∥如果
  ∮(数据32 == 10)
  {
    字符串控制台输出(＆字符串1)
  }
  
  数据32 = 10
  ∥如果
  ∮(数据32 > 10)
  {
    字符串控制台输出(＆字符串2)
  }
  ∥否则如果
  ∝∮(数据32 == 10)
  {
    字符串控制台输出(＆字符串3)
  }
  
  数据32 = 10
  基数32 = 100
  ∥如果
  ∮((数据32 == 10) ∪ (基数32 > 50))
  {
    字符串控制台输出(＆字符串4)
  }
  ∥否则
  ∝
  {
    字符串控制台输出(＆字符串5)
  }
  
  ∥循环语句测试
  整数A = 10
  ∽(整数A> 0)
  {
    整数A--
    字符串控制台输出(＆字符串6)
    ∮(整数A == 5)
    {
      L  ∥退出循环
      
      ∥J 继续下一轮循环
    }
  }
}

∥定义一个控制台输出字符串的函数
∥函数可以定义在调用之后而不需要声明
∫字符串控制台输出(双字 字符串地址)
{
  字符串长度(字符串地址)
  计数32 = 累加32
  获得标准句柄(-11)
  控制台输出(累加32,字符串地址,计数32,0,0)
}

 ∥代码段结束
